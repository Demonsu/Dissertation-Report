\chapter{Pretty Printer}
This chapter mainly focuses on the implementation details of a pretty printer combinator library.

\section{Introduction}

A pretty printer's job is to lay out structured data in a human-friendly way. Take a simple example, a tree of type.

\begin{lstlisting}
data Tree = Node String List[Tree];
\end{lstlisting}

The tree \texttt{Node "aaa" (Cons[Tree] (Node "bbb" (Nil[Tree])) (Nil[Tree]))} is much easier to read if it is presented as:

\begin{lstlisting}
aaa[
    bbb
]
\end{lstlisting}

And a pretty printer library's work is to make the implementation of a pretty printer more convenient. So it's about software reuse. Then the nature of my work is to find specific programming idioms during the process of writing a pretty printer. And these programming idioms are also called combinators. For example, when you want to get the tree layout above, just use several combinators provided by my library:

\begin{lstlisting}
text "aaa" <> (nest 2 (line <> text "bbb"))
\end{lstlisting}

Choosing appropriate layout and converting it into a string at the end are also done by the library. And more specifically, the library should choose a layout which occupies a minimal number of lines(with line width's constraint) while retaining indentation that reflects the underlying structure information.

\emph{Remark}
Note that we are considering the problem of displaying internal data structure into a human-readable form, not improving a layout of an existing text. And surely, we are not going to compete with typesetting systems(such as TEX). However, our library is to strike a balance between sophistication, ease of use and optimality of output.


\section{A Bridge between Data Structure and Layout}

In Oppen's pretty-printer\cite{oppen1980prettyprinting}, the small language he defined serves as the bridge between application specific data structures and pretty layouts. User describes their internal data structures by using the small language. Then the interpreter of Oppen's pretty-printer takes these descriptions as input to calculate the final pretty layout. It looks like that users need to add tags to their data so that the interpreter can recognize it.

When things come to functional programming, algebra has natural advantage to serve as the bridge. As mentioned in Hughes's pretty-printing library\cite{hughes1995design}, his library works with 'pretty documents', of type \textit{Doc}. Take a naive \textit{Doc} for example:

\begin{lstlisting}
Data Doc = NIL
        |  TEXT String Doc
        |  LINE Int Doc
;
\end{lstlisting}

\emph{Remark} Since infix constructors are not supported in F2J. Please be noted that "TEXT" and "LINE" are represented in prefix form.

A pretty-printer will be a function mapping any value to the \textit{Doc} above. And from the definition of the \textit{Doc}, we can see that itself has indentations and line breaks which means the \textit{Doc} knows how to lay itself out prettily. Apparently, some high level combinators should also be offered to users, such as \textit{nest} for generating nested indentations and \textit{showDoc} for converting \textit{Doc} into a string.

\section{A Primitive Pretty Printer}

To begin, we consider the simple case when we just use a naive \textit{Doc}, which means each document will only have one possible layout. To form a library, six high level operators are defined together with it:

\begin{lstlisting}
let nil = NIL
let (<>) (x: Doc) (y: Doc): Doc
let nest (i: Int) (x: Doc): Doc
let text (s: String): Doc
let line: Doc
let showDoc(x: Doc): String
\end{lstlisting}

Here \textbf{<>} is for concatenating two documents. The function \textbf{text} converts a string to the \textit{Doc} and the \textbf{line} denotes a line break. The function \textbf{nest} adds indentation to every new line of the following documents. Finally, the function \textbf{showDoc} converts the \textit{Doc} to a string. Again, take the tree example:

\begin{lstlisting}

Node "aaa"
    (Cons[Tree] (Node "bbbbb" (Nil[Tree]))
    (Cons[Tree] (Node "eee" (Nil[Tree]))
    (Cons[Tree] (Node "ffff" (Nil[Tree]))
    (Nil[Tree]))))

\end{lstlisting}

The above internal data can be printed in below layout:

\begin{lstlisting}
aaa[
    bbbbb,
    eee,
    ffff
]
\end{lstlisting}

The result is just a string containing "\verb|\|n" and " ". What the pretty-printer need to do is to use the six operators we provide above to describe the final result. In here, it's the function which converts a tree to a document.

\begin{lstlisting}
let rec showTree (tree: Tree): Doc=
    case tree of
        Node x xs	-> (text x) <> (showBracket xs)
and
showBracket (tree: List[Tree]): Doc=
    case tree of
            Nil             -> nil
            |	Cons x xs   -> (text "[") <> (nest 2 ((line) <> (showTrees (x +>[Tree] xs))))
                               <> (line) <> (text "]")
and
showTrees (tree: List[Tree]): Doc=
    case tree of
            Nil             -> nil
            |    Cons x xs  ->
    {
    case xs of
            Nil             -> (showTree x)
            |    Cons y ys  -> (showTree x) <> (text ",") <> (line) <> (showTrees (y +>[Tree] ys))
    }
;
\end{lstlisting}
Then the tree can be represented as the following document:
\begin{lstlisting}
text "aaa" <> text "[" <>
nest 2 (
    line <> text "bbbbb" text "," <>
    line <> text "eee" <> text ","
    line <> text "fff" <>
)
line <> text "]"
\end{lstlisting}
It seems not so easy to print the above document. A further reduction can be done with it:
\begin{lstlisting}
text "aaa[" <>
nest 2 line <> text "bbbbb," <>
nest 2 line <> text "eee," <>
nest 2 line <> text "fff" <>
nest 0 line <> text "]"
\end{lstlisting}
Now, the document above is in a normal form which means final layout is easy to get from it. Then the problem focuses on how to derive representations for each function(high level operators). Since each document has only one possible layout, all the operators here should be linear, which means rules can be derived from the definition of these operators directly.
\begin{lstlisting}
let nil = NIL
;

let rec (<>) (x: Doc) (y: Doc): Doc =
    case x of
            TEXT s d    -> TEXT s (d <> y)
        |   LINE i e    -> LINE i (e <> y)
        |   NIL         -> y
;

let rec nest (i: Int) (x: Doc): Doc =
    case x of
            TEXT s d    -> TEXT s (nest i d)
        |   LINE j c    -> LINE (i+j) (nest i c)
        |   NIL         -> NIL
;

let text (s: String): Doc =
    TEXT s NIL
;

let line: Doc =
    LINE 0 NIL
;

let rec showDoc(doc: Doc): String =
    case doc of
            TEXT s x    -> concat s (showDoc x)
        |   LINE i d    -> concat (concat "\n" (space i)) (showDoc d)
        |   NIL         -> ""
;

\end{lstlisting}

\section{A Pretty Printer with Alternative Layouts} \label{section:printer}

In primitive pretty printer, a document was regarded as equivalent to a string. Now, it should be viewed as a set of strings, each corresponding to a different layout of the same document. And what the library need to do is to select a best one from the set.

First, what kind of layout will be the best one? In common, if a maximum line width is given, a prettiest layout always means one fits result onto one line where possible, but introduces sufficient line breaks to keep the total width less than the maximum line width\cite{wadler2003prettier}.

Take a tree data as an example:
\begin{lstlisting}
aaa[
    bbb[
        ee,
        ff
    ],
    cc,
    dd
]
\end{lstlisting}

If the maximum line width is 100 which means there is no limit in this case, then the tree can be printed into one line:
\begin{lstlisting}
aaa[ bbb[ ee, ff ], cc, dd ]
\end{lstlisting}

And if the maximum line width is 20, then the tree should be printed as followed. Because the length of node("bbb")'s children is less than 20, while length of node("aaa")'s is too long to fit into one line.
\begin{lstlisting}
aaa[
    bbb[ ee, ff ],
    cc,
    dd
]
\end{lstlisting}

However, by no means, do we want to see.
\begin{lstlisting}
aaa[
    bbb[ e
    e, ff ],
    cc,dd
]
\end{lstlisting}

This extension is achieved by adding a new high level operator:
\begin{lstlisting}
let group (d: Doc): Doc
\end{lstlisting}

The function group accepts a set of layouts and then returns the set with a new layout added, which everything in the new layout is compressed on one line. That means people can use this interface to tell the library where to compress the line breaks.
For example:
\begin{lstlisting}
group (
    group (
        group (text "hkuplg" <> line <> text "a")
     <> line <> text "b")
<> line <> text "c")
\end{lstlisting}

This will have the following possible layout:
\begin{lstlisting}[language=Haskell]		
hkuplg a b c    hkuplg a b  hkuplg a    hkuplg
                c           b           a
                            c           b
                                        c
\end{lstlisting}

To formalize the semantic of group, two auxiliary operators are added.
\begin{lstlisting}
let (<|>) (x: Doc) (y: Doc): Doc

let flatten (d: Doc): Doc
\end{lstlisting}

The \textbf{<|>} operator represents a union of two layouts. The \textbf{flatten} operator replaces each line break in a layout by a single space. Then a document will always represent a set of layouts where all the layouts in the set flatten to the same one. With \textbf{<|>} and \textbf{flatten}, group can be defined as:
\begin{lstlisting}
let (<|>) (x: Doc) (y: Doc) : Doc =
    UNION x y
;

let rec flatten (d: Doc): Doc =
    case d of
            NIL             -> NIL
        |   LINE i x        -> TEXT " " (flatten x)
        |   TEXT s x        -> TEXT s (flatten x)
        |   UNION x y       -> flatten x
;

let rec group (d: Doc): Doc =
    flatten d <|> d
;
\end{lstlisting}

Then a set of laws can be generated to let these two operators interact with pre-existed operators.
\begin{lstlisting}
(x <|> y) <> z          = (x <> z) <|> (y <> z)
x <> (y <|> z)          = (x <> y) <|> (x <> z)
nest i (x <|> y)        = nest i x <|> nest i y
flatten (x <|> y)       = flatten x
flatten (x <> y)        = flatten x <> flatten y
flatten nil             = nil
flatten (text s)        = text s
flatten line            = text " "
flatten (nest i x)      = flatten x
\end{lstlisting}

Next, how to choose the best layout among the set? Following the method in Hughes' paper\cite{hughes1995design} which specify an ordering relation between lines, it can be done by specifying an ordering relation between documents. Given two lines a,b (a<b) and the available width w.
\begin{lstlisting}
If (b<w)            then b
If (a<w) & (b>w)    then a
If (a>w)            then a
\end{lstlisting}

And in practice, it's done by a function best, which kills all the unions in a document. Additionally, it requires another two parameters: one is the line width w, and the second is spaces k which already occupied on the current line.
\begin{lstlisting}
-- judge whther the doc fit the line width
let rec fits (w: Int) (d: Doc): Bool=
    if w < 0 then False
    else
        case d of
                NIL             -> True
            |   TEXT s x        -> fits (w - s.length()) x
            |   LINE i x        -> True
            |   UNION _  _      -> False
;

-- return a better one, here better means max width within limit
let better (w: Int) (k: Int) (x: Doc) (y: Doc): Doc =
    if fits (w - k) x then x
    else  y
;

-- return the best layout among groups
let rec best (w: Int) (k: Int) (d: Doc): Doc =
    case d of
            NIL             -> NIL
        |   LINE i x        -> LINE i (best w k x)
        |   TEXT s x        -> TEXT s (best w (k + s.length()) x)
        |   UNION x y       -> better w k (best w k x) (best w k y)
;
\end{lstlisting}

Finally, to pretty print a document is just to show the best.
\begin{lstlisting}
 pretty w x    = showDoc (best w 0 x)
\end{lstlisting}

\section{Improving Efficiency}

At first glance, the above implements require time \textit{O(n)}, where \textit{n} is the document's length plus all the count of operators(\textbf{nil, <>, nest, text, line, group}). However, there are two operators slow down the library a lot in time \textit{O($n^2$)}. First, take a look at the function \textbf{nest}:
\begin{lstlisting}
let rec nest (i: Int) (x: Doc): Doc =
    case x of
            TEXT s d    -> TEXT s (nest i d)
        |   LINE j c    -> LINE (i+j) (nest i c)
        |   NIL         -> NIL
;
\end{lstlisting}
For a single nest, it takes \textit{O(n)} to process:
\begin{lstlisting}
nest i ((text s0) <> (text s1)<> (text s2))

TEXT s0 nest i ((text s1) <> (text s2))
TEXT s0 TEXT s1 nest i (text s2)	
TEXT s0 TEXT s1 TEXT s2
\end{lstlisting}
When it comes to a nested nest:
\begin{lstlisting}
nest i0 ((text s0) <> nest i1 ((text s1) <> nest i2 (text s2)))
\end{lstlisting}
It will take \textit{O($n^2$)}. It's quite inefficient for processing a document. And furthermore, when processing a concatenation of documents, it might pile up to the left:
\begin{lstlisting}
let rec (<>) (x: Doc) (y: Doc): Doc =
    case x of
            TEXT s d    -> TEXT s (d <> y)
        |   LINE i e    -> LINE i (e <> y)
        |   NIL         -> y
;

Problem:
(((text s0) <> (text s1)) <> (text s2)) <> (text s3)

Steps:
((TEXT s0 (text s1)) <> (text s2)) <> (text s3)
(TEXT s0 ((text s1) <> (text s2))) <> (text s3)
TEXT s0 (((text s1) <> (text s2)) <> (text s3))
TEXT s0 ((TEXT s1 (text s2)) <> (text s3))
TEXT s0 (TEXT s1 ((text s2) <> (text s3))
TEXT s0 (TEXT s1 (TEXT s2 (text s3)))
TEXT s0 TEXT s1 TEXT s2 TEXT s3 NIL
\end{lstlisting}

A solution for the first problem is to add an explicit data construct in \textit{Doc}, and maintain an indentation when nestings are processed. Then the operator \textbf{nest} will be linear. And the solution for the second problem is very obvious, we can add another explicit data construct for concatenation and modify all
the operators to act on a list of documents. Now, the \textit{Doc} is as follows:
\begin{lstlisting}
data Doc =  NIL
        |   TEXT String
        |   LINE
        |   NEST Int Doc
        |   UNION Doc Doc
        |   CONCAT Doc Doc
;

-- for docpair list, seperating from lazy PList outside
data DList[A] = DNil
              | Consd A (DList[A])
;
\end{lstlisting}
And the function \textbf{<>} and \textbf{nest} will then be quite simple:
\begin{lstlisting}
let (<>) (x: Doc) (y: Doc) : Doc =
    CONCAT x y
;

let nest (i: Int) (x: Doc): Doc =
    NEST i x
;
\end{lstlisting}
However, life will never be easy for all the people. At the same time the function \textbf{<>} and \textbf{nest} are getting easier, there must be someone to handle all the dirty work for them. That's the function \textbf{best}, it now not only needs to choose a best layout from union documents, but also should maintain the indentation for nesting, and what now it deals with is a document list. And furthermore, for not changing the function \textbf{better}, \textbf{fits} and \textbf{showDoc}, it's also expected to generate the naive \textit{Doc} for converting to string. For identical from the \textit{Doc} now, the naive, easy-to-print \textit{Doc} now changes to:
\begin{lstlisting}
data PDoc = NI
        |   TE String PDoc
        |   LI Int PDoc
;
\end{lstlisting}
Finally, let's take a look at \textbf{best}


\begin{lstlisting}
let rec be (w: Int) (k: Int) (docs: DList[Pair]): PDoc =
    case docs of
            DNil                    -> NI
        |   Consd x xs              ->
            {
                case x._2 of
                    NIL             -> be w k xs
                |   CONCAT d1 d2    -> be w k (Consd[Pair] (x._1, d1) (Consd[Pair] (x._1, d2) xs))
                |   NEST j d        -> be w k (Consd[Pair] (j + x._1, d)  xs)
                |   TEXT s          -> TE s (be w k xs)
                |   LINE            -> LI x._1 (be w k xs)
                |   UNION d1 d2     -> better w k (be w k (Consd[Pair] (x._1, d1) xs)) (be w k (Consd[Pair] (x._1, d2) xs))

            }


;

let best (w: Int) (k: Int) (d: Doc): PDoc =

    be w k (Consd[Pair] (0, d) (DNil[Pair]))
;
\end{lstlisting}

The improvement of efficiency should not stay in theory, several experiments are more persuasive. Considering some features of \textit{F2J}, it will always compile before run. And it takes seconds for compilation, which brings too much deviation for analysis. So we need hack a bit into the language. As the language is hosted by JVM, we can just compile the F2J into java classes, then use "JAVA" command to run the object code. Although the loading time of JVM still causes some deviation, at least they compete on a level playing field. Take a look at the test code:
\begin{lstlisting}
let rec looptext (x: Int) : Doc =
    if x > 0 then
        (looptext (x-1)) <> (text "1")
    else
        NIL
;
pretty 30 (looptext 200)
\end{lstlisting}

The test code will generate as many nested texts as you want. However, as \textit{F2J} is a language for academic use, the max count we can hit is 500. Otherwise, either the compiler or the runtime will crash in ways you can't imagine. I use Linux command "time" for evaluating. Raw data is collected in Figure \ref{fig:efficiency comparation}:
\begin{figure}[h!]
    \centering
    \includegraphics[width=0.8\textwidth]{imgs/raw_data}
    \caption{Raw Data}
    \label{fig:efficiency comparation}
\end{figure}
And I draw the raw data into a line chart(Figure \ref{fig:efficiency line chart}), the red one represents the library with efficiency improvement. The x-axis shows how many nested concatenations there are, count in hundreds. And the y-axis shows how much time it takes, count in seconds.
\begin{figure}[h!]
    \centering
    \includegraphics[width=0.8\textwidth]{imgs/compare}
    \caption{Line Chart}
    \label{fig:efficiency line chart}
\end{figure}


\section{Case Studies}

All the case studies in \textbf{Pretty Printer} share the same ADT definition of the case studies in Section\ref{section:mparser} for further integration.

\subsection{Tree Printer}

\subsubsection{Implementation}
"There is no poem as lovely as a tree" -- Joyce Kilmer

So the first case study I did for all the pretty printer library is tree. And it's defined as:

\begin{lstlisting}
data BTree = Node String PList[BTree];
\end{lstlisting}

As discussed in Section\ref{section:printer}. My library provides several basic combinators:
\begin{itemize}
\item text:    for printing string
\item nest:    for indedntation
\item group:   for line breaks compress
\item <>:      for document concatenation
\end{itemize}
I use "[]" to show the hieratical structure between tree nodes, and assuming that nodes on the same level could be printed into one line when the line width is long enough.
\begin{lstlisting}
let bracket (l: String) (d: Doc) (r: String): Doc =
    group (text l <> (nest 2 (line <> d)) <> line <> text r)
;
\end{lstlisting}
Then a printer for the tree can be wrote:
\begin{lstlisting}
let rec showTree (tree: BTree): Doc=
    case tree of
            Node x xs   -> (text x) <> (showBracket xs)
and
showBracket (tree: PList[BTree]): Doc=
    case tree of
            Nil         -> nil
        |   Cons x xs   -> bracket "[" (showTrees (x +>[BTree] xs)) "]"
and
showTrees (tree: PList[BTree]): Doc=
    case tree of
            Nil         -> nil
        |   Cons x xs   ->
        {
            case xs of
                Nil     -> (showTree x)
            |   Cons y ys   -> (showTree x) <> (text ",") <> (line) <> (showTrees (y +>[BTree] ys))
        }
;
\end{lstlisting}

\subsubsection{Test}

For a tree stored in ADT:
\begin{lstlisting}
let tree2 = Node "aaa"
    (Cons[BTree] (Node "bbb" (Cons[BTree] (Node "ee" (Nil[BTree])) (Cons[BTree] (Node "ff" (Nil[BTree])) (Nil[BTree]) ))) (Cons[BTree] (Node "cc" (Nil[BTree])) (Cons[BTree] (Node "dd" (Nil[BTree])) (Nil[BTree]))))
;
\end{lstlisting}

I test the printer with different line width:
\begin{lstlisting}
println "Line Width 10:";
println (pretty 10 (showTree tree2));

println "Line Width 20:";
println (pretty 20 (showTree tree2));

println "Line Width 30:";
println (pretty 30 (showTree tree2))
\end{lstlisting}

My printer will have the output:
\begin{lstlisting}
Line Width 10:
aaa[
  bbb[
    ee,
    ff
  ],
  cc,
  dd
]
Line Width 20:
aaa[
  bbb[ ee, ff ],
  cc,
  dd
]
Line Width 30:
aaa[ bbb[ ee, ff ], cc, dd ]
\end{lstlisting}

\subsection{Simple Arithmetic Expression Printer}

\subsubsection{Implementation}

\subsubsection{Test}

\subsection{XML Printer}

\subsubsection{Implementation}

\subsubsection{Test}

\subsection{Feather Weight Java Printer}

\subsubsection{Implementation}

\subsubsection{Test}

\subsection{A Subset of F2J Printer}

\subsubsection{Implementation}

\subsubsection{Test}



